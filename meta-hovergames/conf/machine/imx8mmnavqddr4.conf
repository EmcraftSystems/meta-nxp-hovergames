#@TYPE: Machine
#@NAME: NXP i.MX 8M Mini NavQ board with DDR4 memory
#@SOC: i.MX8MM
#@DESCRIPTION: Machine configuration for NXP i.MX 8M Mini NavQ board with DDR4 memory
#@MAINTAINER: Jun Zhu <junzhu@nxp.com>

MACHINEOVERRIDES =. "mx8:mx8m:mx8mm:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa53.inc

MACHINE_FEATURES += "pci wifi bluetooth optee qca9377 bcm43455 bcm4356 bcm4359 jailhouse"

KERNEL_DEVICETREE = "freescale/imx8mm-navq-ddr4.dtb"

WKS_FILE_DEPENDS += "imx-m4-demos"
IMAGE_BOOT_FILES += " \
    imx8mm_m4_TCM_hello_world.bin \
    imx8mm_m4_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mm_m4_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mm_m4_TCM_sai_low_power_audio.bin \
"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "imx8mm_navq_ddr4_config,sdcard"

SPL_BINARY = "spl/u-boot-spl.bin"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = " \
    ddr4_imem_1d.bin \
    ddr4_dmem_1d.bin \
    ddr4_imem_2d.bin \
    ddr4_dmem_2d.bin \
"

# Set u-boot DTB
UBOOT_DTB_NAME = "imx8mm-navq-ddr4.dtb"

# Set imx-mkimage boot target
IMXBOOT_TARGETS = "flash_navq_ddr4"

# Set Serial console
SERIAL_CONSOLES = "115200;ttymxc1"

IMAGE_BOOTLOADER = "imx-boot"

LOADADDR = ""
IMX_BOOT_SEEK = "33"
